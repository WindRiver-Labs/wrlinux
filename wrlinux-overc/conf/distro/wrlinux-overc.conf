#@TYPE: Distro
#@NAME: Wind River Linux OverC
#@DESCRIPTION: Wind River Linux (OverC)

# Temporary workaround, kernel 5.0 is now gone, 5.2 is standard
PREFERRED_VERSION_linux-yocto ?= "5.2%"
PREFERRED_VERSION_linux-yocto_qemux86 ?= "5.2%"
PREFERRED_VERSION_linux-yocto_qemux86-64 ?= "5.2%"
PREFERRED_VERSION_linux-yocto_qemuarm ?= "5.2%"
PREFERRED_VERSION_linux-yocto_qemumips ?= "5.2%"
PREFERRED_VERSION_linux-yocto_qemumips64 ?= "5.2%"
PREFERRED_VERSION_linux-yocto_qemuppc ?= "5.2%"

# Use the overc distribution as-is.  Do not change distribution configuration
# in this file!
require conf/distro/overc.conf

DISTROOVERRIDES_append = "${@['', ':'][(d.getVar('DISTRO', True) != '')]}osv-wrlinux"

# We want to globally disable wic for any overc usage.  OverC has it's own
# image generation method.
IMAGE_FSTYPES_remove = "wic wic.bmap"

# 'unlock' the build-sysroots recipe from signature checking
SIGGEN_UNLOCKED_RECIPES ?= "build-sysroots"

# This is the one exception, for branding purposes
DISTRO_NAME = "Wind River Linux OverC"

require conf/distro/include/wrlinux-overc-blacklist.conf
require conf/distro/include/wrlinux-overc-whitelist.conf

# Enforce our configuration as immutable
OE_IMPORTS += "overc.immutable"
BB_SIGNATURE_HANDLER = "OverCBasicHash"

include ${@'conf/distro/include/${MACHINE}-lockdown.inc' if (not d.getVar('SIGGEN_OVERC_DEVEL') or d.getVar('SIGGEN_OVERC_DEVEL') == '0') else ''}

BANNER[wrlinux-overc_distro_A] = "${@'Development Mode Enabled, ignoring hashes' if (d.getVar('SIGGEN_OVERC_DEVEL') and d.getVar('SIGGEN_OVERC_DEVEL') != '0') else ''}"

# Load our package revision information
include ${@'conf/distro/include/pr-lockdown.inc' if (not d.getVar('PRSERV_OVERC_DEVEL') or d.getVar('PRSERV_OVERC_DEVEL') == '0') else''}

BANNER[wrlinux-overc_distro_B] = "${@'Development Mode Enabled, ignoring locked PR values' if (d.getVar('PRSERV_OVERC_DEVEL') and d.getVar('PRSERV_OVERC_DEVEL') != '0') else ''}"
