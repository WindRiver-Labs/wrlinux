#
# Copyright (C) 2013-2015 Wind River Systems, Inc.
#
DISTRO_NAME = "Wind River Linux"
SDK_VENDOR = "-wrlinuxsdk"
WRLINUX_VERSION = "9.0"

SDK_VERSION := "${WRLINUX_VERSION}"
WRLINUX_RCPLVERSION ?= ".0"
WRLINUX_EXTRAVERSION ?= ".0${WRLINUX_RCPLVERSION}"
DISTRO_VERSION = "${WRLINUX_VERSION}${WRLINUX_EXTRAVERSION}"
# When the DISTRO_VERSION contains DATE the following must be used
DISTRO_VERSION[vardepsexclude] += "DATE"

# If you're using glibc-std-sato, DISTRO will be wrlinux-std-sato.
# osv-wrlinux is used for settings we want across our whole product,
# not just in a specific distro.
DISTROOVERRIDES_append = "${@['', ':'][(d.getVar('DISTRO', True) != '')]}osv-wrlinux"

# Use -O0 for debugging; preserve all debug types
DEBUG_FLAGS_osv-wrlinux = "-g"
DEBUG_OPTIMIZATION_osv-wrlinux = "-O -fno-omit-frame-pointer ${DEBUG_FLAGS} -pipe"

# Add profiling optimization type
PROFILING_OPTIMIZATION = "${FULL_OPTIMIZATION} -fno-omit-frame-pointer -fvisibility=default"
SELECTED_OPTIMIZATION[vardeps] += "PROFILING_OPTIMIZATION"

# wrlinux specific versions to avoid -fpermissive in bitbake.conf
BUILD_CXXFLAGS_osv-wrlinux = "${BUILD_CFLAGS}"
TARGET_CXXFLAGS_osv-wrlinux = "${TARGET_CFLAGS}"

# we want to use rsyslog as the default syslog, but it's GPLv3, so need this
# check to switch to sysklogd if GLPv3 is blacklisted.
VIRTUAL-RUNTIME_syslog ?= "${@bb.utils.contains('INCOMPATIBLE_LICENSE', 'GPLv3', 'sysklogd', 'rsyslog', d)}"

# common wrlinux distro definitions
#
require wrlinux-common.inc

# override value set in default-distrovars.inc which are --
#
#DISTRO_FEATURES = "alsa argp bluetooth ext2 irda largefile \
#                    pcmcia usbgadget usbhost wifi xattr nfs \
#                    zeroconf pci 3g x11 ${DISTRO_FEATURES_LIBC}"
#
DISTRO_FEATURES ?= "\
        alsa \
        argp \
        bluetooth \
        ext2 \
        irda \
        largefile \
        nfs \
        pam \
        pcmcia \
        pci \
        pulseaudio \
        usbgadget \
        usbhost \
        wifi \
        xattr \
        ${DISTRO_FEATURES_LIBC} \
        multiarch \
"

# Workaround for native packages depending on DISTRO_FEATURES in 1.5
# Create a fixed list of distro features for all native packages
# so that the sstate can be shared. This is the union of all
# DISTRO_FEATURES for glibc-tiny, core, small, std and std-sato.
# I used DISTRO_FEATURES_LIBC_DEFAULT because glibc-tiny
# changes the DISTRO_FEATURES_LIBC
DISTRO_FEATURES_class-native_forcevariable="\
        alsa \
        argp \
        bluetooth \
        ext2 \
        irda \
        largefile \
        nfs \
        pam \
        pcmcia \
        pci \
        pulseaudio \
        usbgadget \
        usbhost \
        wifi \
        sysvinit \
        x11 \
        opengl \
        ${DISTRO_FEATURES_LIBC_DEFAULT}"

# Native sstate should not depend on multilib provider whitelist
MULTI_PROVIDER_WHITELIST_class-native_forcevariable=""

# Disable trying to run ldconfig as a package post install rule
USE_LDCONFIG ?= "0"
# As ldconfig is missing on target, disable the corresponding PACKAGECONFIG for systemd
PACKAGECONFIG_remove_pn-systemd = "${@['ldconfig', ''][int(d.getVar('USE_LDCONFIG', True))]}"

# Configure the root user's home directory
ROOT_HOME ?= "/root"

# initramfs image for wrlinux distro
INITRD_IMAGE_LIVE ?= "wrlinux-image-minimal-initramfs"

# The providers are fftwf, fftwl, fftw
PREFERRED_PROVIDER_virtual/fftw = "fftw"

# Set the default psplash from psplash-default to psplash-windriver
SPLASH = "psplash-windriver"

# Possibly eliminate busybox from an image.  The variables defined here
# are used by oe-core packagegroup-core-boot.bb.  The overrides are
# DISTRO names.

VIRTUAL-RUNTIME_login_manager = "shadow"
VIRTUAL-RUNTIME_login_manager_wrlinux-tiny = "busybox"
VIRTUAL-RUNTIME_login_manager_wrlinux-small = "busybox"

# We assume that we provide base-utils.  This variable must
# empty or one item.
#
VIRTUAL-RUNTIME_base-utils = ""
VIRTUAL-RUNTIME_base-utils_wrlinux-tiny = "busybox"
VIRTUAL-RUNTIME_base-utils_wrlinux-small = "busybox"

# This runs the busybox-hwclock script during systemv
# init.  This does NOT supply /sbin/hwclock, only a script!
#
VIRTUAL-RUNTIME_base-utils-hwclock = "busybox-hwclock"

# Supply replacements for busybox functions we really, really need.
#
VIRTUAL-RUNTIME_no-busybox = "dpkg-start-stop"
VIRTUAL-RUNTIME_no-busybox_wrlinux-tiny = ""
VIRTUAL-RUNTIME_no-busybox_wrlinux-small = ""

IMAGE_INSTALL_append = " ${VIRTUAL-RUNTIME_no-busybox}"

