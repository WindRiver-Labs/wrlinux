#
# Copyright (C) 2013 Wind River Systems, Inc.
#
DISTRO_NAME = "Wind River Linux"
SDK_VENDOR = "-wrlinuxsdk"
WRLINUX_VERSION = "6.0"
SDK_VERSION := "${WRLINUX_VERSION}-${MACHINE}"
WRLINUX_RCPLVERSION ?= ".0"
WRLINUX_EXTRAVERSION ?= ".0${WRLINUX_RCPLVERSION}"
DISTRO_VERSION = "6.0${WRLINUX_EXTRAVERSION}"
# When the DISTRO_VERSION contains DATE the following must be used
DISTRO_VERSION[vardepsexclude] += "DATE"

# If you're using glibc-std-sato, DISTRO will be wrlinux-std-sato.
# osv-wrlinux is used for settings we want across our whole product,
# not just in a specific distro.
DISTROOVERRIDES = "${@['', d.getVar('DISTRO', True) + ':'][(d.getVar('DISTRO', True) != '')]}osv-wrlinux"

# Use -O0 for debugging; preserve all debug types
DEBUG_FLAGS_osv-wrlinux = "-g"
DEBUG_OPTIMIZATION_osv-wrlinux = "-O0 -fno-omit-frame-pointer ${DEBUG_FLAGS} -pipe"

# Add profiling optimization type
PROFILING_OPTIMIZATION = "${FULL_OPTIMIZATION} -fno-omit-frame-pointer -fvisibility=default"
SELECTED_OPTIMIZATION[vardeps] += "PROFILING_OPTIMIZATION"

# arm needs -mapcs-frame for debugging and profiling
DEBUG_OPTIMIZATION_append_arm = " -mapcs-frame"
PROFILING_OPTIMIZATION_append_arm = " -mapcs-frame"

# wrlinux specific versions to avoid -fpermissive in bitbake.conf
BUILD_CXXFLAGS_osv-wrlinux = "${BUILD_CFLAGS}"
TARGET_CXXFLAGS_osv-wrlinux = "${TARGET_CFLAGS}"

# common wrlinux distro definitions
#
require wrlinux-common.inc

# override value set in default-distrovars.inc which are --
#
#DISTRO_FEATURES = "alsa argp bluetooth ext2 irda largefile \
#                    pcmcia usbgadget usbhost wifi xattr nfs \
#                    zeroconf pci 3g x11 ${DISTRO_FEATURES_LIBC}"
#
DISTRO_FEATURES = "\
        alsa \
        argp \
        bluetooth \
        ext2 \
        irda \
        largefile \
        nfs \
        pam \
        pcmcia \
        pci \
        pulseaudio \
        usbgadget \
        usbhost \
        wifi \
        xattr \
        ${DISTRO_FEATURES_TEMP} \
        ${DISTRO_FEATURES_LIBC}"

# These are just needed for world builds, at least for now.
# Graphics images are expected to be built with the wrlinux-std-sato
# distro.
#
DISTRO_FEATURES_TEMP = " x11 opengl"

# Workaround for native packages depending on DISTRO_FEATURES in 1.5
# Create a fixed list of distro features for all native packages
# so that the sstate can be shared. This is the union of all
# DISTRO_FEATURES for glibc-tiny, core, small, std and std-sato.
# I used DISTRO_FEATURES_LIBC_DEFAULT because glibc-tiny
# changes the DISTRO_FEATURES_LIBC
DISTRO_FEATURES_class-native="\
        alsa \
        argp \
        bluetooth \
        ext2 \
        irda \
        largefile \
        nfs \
        pam \
        pcmcia \
        pci \
        pulseaudio \
        usbgadget \
        usbhost \
        wifi \
        sysvinit \
        x11 \
        opengl \
        ${DISTRO_FEATURES_LIBC_DEFAULT}"

# Disable trying to run ldconfig as a package post install rule
USE_LDCONFIG ?= "0"

# Configure the root user's home directory
ROOT_HOME ?= "/root"
