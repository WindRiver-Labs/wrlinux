#
# Copyright (C) 2013-2017 Wind River Systems, Inc.
#
# We have a conf and classes directory, add to BBPATH
BBPATH =. "${LAYERDIR}:"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

# Active some changes dynamically based on layer inclusion
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

BBFILE_COLLECTIONS += "wrlinux"
BBFILE_PATTERN_wrlinux = "${LAYERDIR}/"
BBFILE_PRIORITY_wrlinux = "6"

# Add scripts to PATH
PATH .= ":${LAYERDIR}/scripts"

LAYER_PATH_wrlinux = "${LAYERDIR}"

# We have a LICENSE_DIR to search for additional licenses
WR_EXTRA_LIC_DIR = "${LAYERDIR}/files/common-licenses"
LICENSE_PATH[vardepsexclude] += "WR_EXTRA_LIC_DIR"
LICENSE_PATH += "${WR_EXTRA_LIC_DIR}"

# This should only be incremented on significant changes that will
# cause compatibility issues with other layers
LAYERVERSION_wrlinux = "2"

LAYERDEPENDS_wrlinux = " \
	core \
	wr-template \
	"

LAYERRECOMMENDS_wrlinux = " \
	gnome-layer \
	networking-layer \
	filesystems-layer \
	openembedded-layer \
	webserver \
	xfce-layer \
	virtualization-layer \
	realtime \
	wrlinux-dl \
	dpdk \
	"

# Enable system wide wrlinux/blacklist behavior
require wrlinux-version.inc
require wrlinux-blacklist.conf
require wrlinux-whitelist.conf
require wrlinux-exclude-from-world.conf

# We want to use the LOCAL versions of linux-yocto...
BB_NO_NETWORK_pn-linux-yocto = "1"
BB_NO_NETWORK_pn-linux-yocto-rt = "1"
BB_NO_NETWORK_pn-linux-yocto-tiny = "1"

PREMIRRORS_prepend = " \
     git://git.yoctoproject.org/linux-yocto-4.12.git git://${LAYERDIR}/git/linux-yocto-4.12.git;protocol=file \n \
     git://git.yoctoproject.org/linux-kernel-cache git://${LAYERDIR}/git/yocto-kernel-cache.git;protocol=file \n \
"

