#
# Copyright (C) 2013-2019 Wind River Systems, Inc.
# All rights reserved.
#
# Distribution definition for: wrlinux-tiny

#@TYPE: Distro
#@NAME: Wind River Linux Tiny
#@DESCRIPTION: Wind River Linux (Example minimal distribution, will require customization)

# wrlinux-tiny is intended to define a tiny Linux system comprised of a
# Linux kernel tailored to support each specific MACHINE and busybox.
# wrlinux-tiny sets some basic policy to ensure a usable system while still
# keeping the rootfs and kernel image as small as possible.
#
# The policies defined are intended to meet the following goals:
# o Serial consoles only (no framebuffer or VGA console)
# o Basic support for IPV4 networking
# o Single user ash shell
# o Static images (no support for adding packages or libraries later)
# o Read-only or RAMFS root filesystem
# o Combined Linux kernel + rootfs in under 4MB
#

WRLINUX_VARIANT = "tiny"

require conf/distro/wrlinux.conf

# Distro config is evaluated after the machine config, so we have to explicitly
# set the kernel provider to override a machine config.
PREFERRED_PROVIDER_virtual/kernel = "linux-yocto-tiny"
PREFERRED_VERSION_linux-yocto-tiny ?= "4.18%"

# We can use packagegroup-core-boot, but in the future we may need a new packagegroup-core-tiny
#WRLINUX_DEFAULT_EXTRA_RDEPENDS += "packagegroup-core-boot"
# Drop kernel-module-af-packet from RRECOMMENDS
WRLINUX_DEFAULT_EXTRA_RRECOMMENDS = ""

# Drop native language support. This removes the
# glibc->bash->gettext->libc-posix-clang-wchar dependency.
USE_NLS="no"

# -dev packages require pkgconfig, but it pulls in glib->dbus->shadow
# which breaks with tiny's minimal libc.
# -dev packages are not currently supported on wrlinux-tiny.
ASSUME_PROVIDED += "pkgconfig$"

# Comment out any of the lines below to disable them in the build
# DISTRO_FEATURES options:
# alsa bluetooth ext2 irda pcmcia usbgadget usbhost wifi nfs zeroconf pci
DISTRO_FEATURES_TINY = "pci"
DISTRO_FEATURES_NET = "ipv4 ipv6"
DISTRO_FEATURES_USB = "usbhost"
#DISTRO_FEATURES_USBGADGET = "usbgadget"
DISTRO_FEATURES_WIFI = "wifi"

DISTRO_FEATURES = "${DISTRO_FEATURES_TINY} \
                   ${DISTRO_FEATURES_NET} \
                   ${DISTRO_FEATURES_USB} \
                   ${DISTRO_FEATURES_USBGADGET} \
                   ${DISTRO_FEATURES_WIFI} \
                  "

# Enable LFS - see bug YOCTO #5865
DISTRO_FEATURES_append_libc-musl = " largefile"

# From poky:
WRLINUX_DEFAULT_DISTRO_FEATURES = "largefile opengl ptest multiarch wayland vulkan"

DISTRO_FEATURES_class-native = "${DISTRO_FEATURES_DEFAULT} ${WRLINUX_DEFAULT_DISTRO_FEATURES}"
DISTRO_FEATURES_class-nativesdk = "${DISTRO_FEATURES_DEFAULT} ${WRLINUX_DEFAULT_DISTRO_FEATURES}"

# enable mdev/busybox for init
VIRTUAL-RUNTIME_dev_manager = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"
VIRTUAL-RUNTIME_init_manager = "busybox"
VIRTUAL-RUNTIME_initscripts = "initscripts"
VIRTUAL-RUNTIME_keymaps = "keymaps"
DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"
VIRTUAL-RUNTIME_no-busybox = ""

# FIXME: Consider adding "modules" to MACHINE_FEATURES and using that in
# packagegroup-core-base to select modutils-initscripts or not.  Similar with "net" and
# netbase.

# By default we only support initramfs and tar.bz2. We don't build live as that
# pulls in a lot of dependencies for the live image and the installer, like
# udev, grub, etc.  These pull in gettext, which fails to build with wide
# character support. 
IMAGE_FSTYPES += "cpio.gz tar.bz2"

# Drop v86d from qemu dependency list (we support serial)
# Drop grub from meta-intel BSPs
# FIXME: A different mechanism is needed here. We could define -tiny
#        variants of all compatible machines, but that leads to a lot
#        more machine configs to maintain long term.
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

# The mtrace script included by glibc is a perl script. This means the system
# will build perl in case this package is installed. Since we don't care about
# this script for the purposes of tiny, remove the dependency from here.
RDEPENDS_${PN}-mtrace_pn-glibc = ""

INHERIT += "blacklist"
PNBLACKLIST[build-appliance-image] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-base] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-clutter] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-full-cmdline] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-lsb] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-lsb-dev] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-lsb-sdk] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-rt] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-rt-sdk] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-sato] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-sato-dev] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-sato-sdk] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-x11] = "not buildable with wrlinux-tiny"
PNBLACKLIST[core-image-weston] = "not buildable with wrlinux-tiny"
PNBLACKLIST[wrlinux-image-glibc-small] = "not buildable with wrlinux-tiny"
PNBLACKLIST[wrlinux-image-glibc-core] = "not buildable with wrlinux-tiny"
PNBLACKLIST[wrlinux-image-glibc-std] = "not buildable with wrlinux-tiny"
PNBLACKLIST[wrlinux-image-glibc-std-sato] = "not buildable with wrlinux-tiny"
PNBLACKLIST[wrlinux-image-glibc-cgl] = "not buildable with wrlinux-tiny"

# Disable python usage in opkg-utils since it won't build with tiny config
PACKAGECONFIG_pn-opkg-utils = "update-alternatives"
PACKAGECONFIG_pn-lib32-opkg-utils = "update-alternatives"

TEMPLATES_PATH = "${LAYER_PATH_wrlinux}/wrlinux-distro/templates"
WRTEMPLATE_append = " feature/posix-timers"
