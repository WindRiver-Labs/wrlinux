#
# Copyright (C) 2013-2017 Wind River Systems, Inc.
#
# This file is probably included by all the other wrlinux distro conf's.
#

#@TYPE: Distro
#@NAME: Wind River Linux
#@DESCRIPTION: Wind River Linux (Full Featured Distribution)

# Add profiling optimization type
PROFILING_OPTIMIZATION = "${FULL_OPTIMIZATION} -fno-omit-frame-pointer -fvisibility=default"
SELECTED_OPTIMIZATION[vardeps] += "PROFILING_OPTIMIZATION"
# fix linking/relocating error
PROFILING_OPTIMIZATION_remove_pn-llvm_powerpc = "-fno-omit-frame-pointer"

# we want to use rsyslog as the default syslog, but it's GPLv3, so need this
# check to switch to sysklogd if GLPv3 is blacklisted.
VIRTUAL-RUNTIME_syslog ?= "${@bb.utils.contains('INCOMPATIBLE_LICENSE', 'GPLv3', 'sysklogd', 'rsyslog', d)}"

# common wrlinux distro definitions
#
require wrlinux-common.inc

# override value set in default-distrovars.inc which are --
#
#DISTRO_FEATURES = "alsa argp bluetooth ext2 irda largefile \
#                    pcmcia usbgadget usbhost wifi xattr nfs \
#                    zeroconf pci 3g x11 ${DISTRO_FEATURES_LIBC}"
#
DISTRO_FEATURES ?= "\
        3g \
        acl \
        alsa \
        argp \
        bluetooth \
        ext2 \
        irda \
        largefile \
        ldconfig \
        nfc \
        nfs \
        pam \
        pcmcia \
        pci \
        pulseaudio \
        usbgadget \
        usbhost \
        wifi \
        xattr \
        zeroconf \
        ${DISTRO_FEATURES_LIBC} \
        multiarch \
        virtualization \
"

# Workaround for native packages depending on DISTRO_FEATURES in 1.5
# Create a fixed list of distro features for all native packages
# so that the sstate can be shared. This is the union of all
# DISTRO_FEATURES for glibc-tiny, core, small, std and std-sato.
# I used DISTRO_FEATURES_LIBC_DEFAULT because glibc-tiny
# changes the DISTRO_FEATURES_LIBC
DISTRO_FEATURES_class-native_forcevariable="\
        alsa \
        argp \
        bluetooth \
        ext2 \
        irda \
        largefile \
        nfs \
        pam \
        pcmcia \
        pci \
        pulseaudio \
        usbgadget \
        usbhost \
        wifi \
        sysvinit \
        x11 \
        opengl \
        ${DISTRO_FEATURES_LIBC_DEFAULT}"

# Native sstate should not depend on multilib provider whitelist
MULTI_PROVIDER_WHITELIST_class-native_forcevariable=""

# Configure the root user's home directory
ROOT_HOME ?= "/root"

# initramfs image for wrlinux distro
INITRD_IMAGE_LIVE ?= "${@d.getVar('INITRAMFS_IMAGE', True) if d.getVar('INITRAMFS_IMAGE', True) else 'wrlinux-image-minimal-initramfs'}"

# The providers are fftwf, fftwl, fftw
PREFERRED_PROVIDER_virtual/fftw = "fftw"

# Set the default psplash from psplash-default to psplash-windriver
SPLASH = "psplash-windriver"

## Syslinux options for boot menus when using isolinux or linux live
SYSLINUX_TIMEOUT = "50"
SYSLINUX_SPLASH = "${@oe.utils.conditional('WRLINUX_BRANCH', 'LTS', '${LAYER_PATH_wrlinux}/files/syslinux/bootloading-blue-wrl.png', '${LAYER_PATH_wrlinux}/files/syslinux/bootloading-gray-wrl.png', d)}"
AUTO_SYSLINUXMENU = "1"

## Have graphics and serial in the boot menu when use grub-efi in iso
GRUB_GFXSERIAL = "1"
GRUB_TIMEOUT = "5"

# Possibly eliminate busybox from an image.  The variables defined here
# are used by oe-core packagegroup-core-boot.bb.  The overrides are
# DISTRO names.
VIRTUAL-RUNTIME_login_manager = "shadow"

# We assume that we provide base-utils.  This variable must
# empty or one item.
#
VIRTUAL-RUNTIME_base-utils = ""

# This runs the busybox-hwclock script during systemv
# init.  This does NOT supply /sbin/hwclock, only a script!
#
VIRTUAL-RUNTIME_base-utils-hwclock = "busybox-hwclock"

# Supply replacements for busybox functions we really, really need.
#
VIRTUAL-RUNTIME_no-busybox = "packagegroup-busybox-replacement"

IMAGE_INSTALL_append = " ${VIRTUAL-RUNTIME_no-busybox}"

# Explicitly specify which kernel we use for CI
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto-dev"

INHERIT += "ext_secure_cflags wrlinux-metadata_scm"

require conf/distro/include/security_flags_wrs.inc

#set wrlinux network manager
NETWORK_MANAGER = "bind"
