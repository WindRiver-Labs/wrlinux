#
# Copyright (C) 2013-2020 Wind River Systems, Inc.
#
# This file is probably included by all the other wrlinux distro conf's.
#

#@TYPE: Distro
#@NAME: Wind River Linux
#@DESCRIPTION: Wind River Linux (Full Featured Distribution)

# Add profiling optimization type
PROFILING_OPTIMIZATION = "${FULL_OPTIMIZATION} -fno-omit-frame-pointer -fvisibility=default"
SELECTED_OPTIMIZATION[vardeps] += "PROFILING_OPTIMIZATION"

# Some packages have problems with PROFILING_OPTIMIZATION
#
# fix linking/relocating error
PROFILING_OPTIMIZATION_remove_pn-llvm_powerpc = "-fno-omit-frame-pointer"
# fix error: r7 cannot be used in asm here
PROFILING_OPTIMIZATION_remove_pn-pulseaudio_arm = "-fno-omit-frame-pointer"
# gcc-sanitizers makes extensive use of hidden visibility and frame pointer omission.
# Trying to override that can cause build failures.
PROFILING_OPTIMIZATION_pn-gcc-sanitizers = "${FULL_OPTIMIZATION}"
# Fix traps: python[550] trap invalid opcode ip:b7f80f00 sp:bfb09a10 error:0 in ld-2.30.so[b7f7b000+1c000]
PROFILING_OPTIMIZATION_remove_pn-glibc_x86 = "-fno-omit-frame-pointer"

# we want to use rsyslog as the default syslog, but it's GPLv3, so need this
# check to switch to sysklogd if GLPv3 is blacklisted.
VIRTUAL-RUNTIME_syslog ?= "${@bb.utils.contains('INCOMPATIBLE_LICENSE', 'GPLv3', 'sysklogd', 'rsyslog', d)}"
VIRTUAL-RUNTIME_base-utils-syslog ?= "${VIRTUAL-RUNTIME_syslog}"


# common wrlinux distro definitions
#
require wrlinux-common.inc

# Add general dependency
DISTRO_FEATURES_DEFAULT_remove_class-target = "x11"
WRLINUX_DEFAULT_DISTRO_FEATURES += "pam"

# Native sstate should not depend on multilib provider whitelist
MULTI_PROVIDER_WHITELIST_class-native_forcevariable=""

# Configure the root user's home directory
ROOT_HOME ?= "/root"

# initramfs image for wrlinux distro
INITRD_IMAGE_LIVE ?= "${@d.getVar('INITRAMFS_IMAGE', True) if d.getVar('INITRAMFS_IMAGE', True) else 'wrlinux-image-minimal-initramfs'}"

# The providers are fftwf, fftwl, fftw
PREFERRED_PROVIDER_virtual/fftw = "fftw"

# Set the default psplash from psplash-default to psplash-windriver
SPLASH = "psplash-windriver"

## Syslinux options for boot menus when using isolinux or linux live
SYSLINUX_TIMEOUT = "50"
SYSLINUX_SPLASH = "${@oe.utils.conditional('WRLINUX_BRANCH', 'LTS', '${LAYER_PATH_wrlinux}/files/syslinux/bootloading-blue-wrl.png', '${LAYER_PATH_wrlinux}/files/syslinux/bootloading-gray-wrl.png', d)}"
AUTO_SYSLINUXMENU = "1"

## Have graphics and serial in the boot menu when use grub-efi in iso
GRUB_GFXSERIAL = "1"
GRUB_TIMEOUT = "5"

# Possibly eliminate busybox from an image.  The variables defined here
# are used by oe-core packagegroup-core-boot.bb.  The overrides are
# DISTRO names.
VIRTUAL-RUNTIME_login_manager = "shadow"

# We assume that we provide base-utils.  This variable must
# empty or one item.
#
VIRTUAL-RUNTIME_base-utils = ""

# This runs the busybox-hwclock script during systemv
# init.  This does NOT supply /sbin/hwclock, only a script!
#
VIRTUAL-RUNTIME_base-utils-hwclock = "busybox-hwclock"

# Supply replacements for busybox functions we really, really need.
#
VIRTUAL-RUNTIME_no-busybox = "packagegroup-busybox-replacement"

IMAGE_INSTALL_append = " ${VIRTUAL-RUNTIME_no-busybox}"

# Explicitly specify which kernel we use for CI
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"

INHERIT += "ext_secure_cflags wrlinux-metadata_scm"

require conf/distro/include/security_flags_wrs.inc

#set wrlinux network manager
NETWORK_MANAGER = "bind"

# Support distro features override, such as cgl
INHERIT += "distrooverrides"
DISTRO_FEATURES_OVERRIDES += "cgl"

# Support DISTRO_FEATURES(cgl)
require wrlinux-cgl/conf/distro_features/distro_feature_cgl.inc

# Define WR sample keys directory
WR_SAMPLE_KEYS_DIR ?= "${LAYER_PATH_wrlinux-distro}/files/sample-keys"

# By default the sample keys are used
WR_KEYS_DIR ?= "${WR_SAMPLE_KEYS_DIR}"

PREFERRED_VERSION_linux-yocto ?= "5.4%"
